plugins {
  // https://github.com/bintray/gradle-bintray-plugin/issues/267
  id "com.jfrog.bintray" version "1.8.5"
}

def PUBLISH_PROJECTS = [
  'java-reselect',
]

allprojects {
  apply plugin: 'java'
  apply plugin: 'ivy-publish'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  version = "${version_reselect}" // see gradle.properties

  repositories {
    mavenCentral()
  }

  test {
    useJUnitPlatform()
  }

  dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
  }

  if (PUBLISH_PROJECTS.contains(project.name)) {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'
    apply plugin: "ivy-publish"

    bintray {
      user = System.getenv('BINTRAY_USER')
      key = System.getenv('BINTRAY_KEY')
      publications = ['MyPublication']
      publish = true

      pkg {
        repo = 'maven'
        name = project.name
        userOrg = System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pcdv/java-reselect.git'
        websiteUrl = 'https://github.com/pcdv/java-reselect'
        issueTrackerUrl = 'https://github.com/pcdv/java-reselect/issues'
        description = project.description
        version {
          name = "${version_reselect}"
          desc = project.description
          released = new Date()
        }
      }
    }


    publishing {
      repositories {
        ivy {
          name "local"
          url "${System.properties['user.home']}/.ivy2/local"
        }
      }
      publications {
        // allows to do a local publish
        ivy(IvyPublication) {
          organisation "com.github.pcdv.java-reselect"
          from components.java
        }
        MyPublication(MavenPublication) {
          from components.java
          groupId 'com.github.pcdv.java-reselect'
          artifactId project.name
          version "${version}"

//          artifact sourceJar {
//            classifier "sources"
//          }
        }
      }
    }
  }

//  task sourceJar(type: Jar) {
//    from sourceSets.main.allJava
//  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
        'Implementation-Version': project.version
    }
  }
}
